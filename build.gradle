import se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
  id "com.gradle.build-scan" version "1.11"
  id "com.matthewprenger.cursegradle" version "1.0.9"
  id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.55"
}
apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'

group= "zmaster587.advancedRocketry" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "AdvancedRocketry"
sourceCompatibility = 8
targetCompatibility = 8

jar {
        manifest {
        attributes 'FMLCorePlugin': 'zmaster587.advancedRocketry.asm.AdvancedRocketryPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
}

apply from: "gradle/scripts/getbuildnumber.gradle"
minecraft {
    version = "1.12.2-14.23.1.2602"
    runDir = "run"
    mappings = "snapshot_20180122"
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    replaceIn "src/main/java/zmaster587/advancedRocketry/AdvancedRocketry.java"
    replace "@MAJOR@", version_major
    replace "@MINOR@", version_minor
    replace "@REVIS@", version_revis
    replace "@BUILD@", project.getBuildNumber()
    
    replace "%LIBVULPESVERSION%", project.libVulpesVersion
}

apply from: "gradle/scripts/version.gradle"

repositories {
    flatDir {
        dirs 'libs'
    }
    maven {
        name = "mezz.jei"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        url = "http://maven.mcmoddev.com"
    }
    ivy {
            name "industrialcraft-2"
            artifactPattern "http://jenkins.ic2.player.to/job/IC2_112/42/artifact/build/libs/[module]-[revision].[ext]"
        }
//    ivy {
//            name "EnderIO"
//            artifactPattern "https://addons-origin.cursecdn.com/files/2238/375/[module]-[revision].[ext]"
//        }
    ivy {
            name "LibVulpes"
            artifactPattern "http://65.78.115.159:8080/job/LibVulpes/job/libVulpes/job/1.12/" + project.libVulpesBuildNum + "/artifact/output/[module]-[revision].[ext]"
        }
}

dependencies {
    compile name: 'industrialcraft-2', version: '2.8.42-ex112-api', ext: 'jar'
    compile "net.darkhax.tesla:Tesla-1.12.2:1.0.63:deobf"
    //compile name: 'EnderIO', version: '1.7.10-2.2.8.381', ext: 'jar'
    compile files('libs/LibVulpes-1.12.2-0.2.7-deobf.jar')
    
    // compile against the JEI API
    deobfCompile "mezz.jei:jei_1.12.2:4.8.5.147:api"
    // at runtime, use the full JEI jar
    runtime "mezz.jei:jei_1.12.2:4.8.5.147"

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
     from sourceSets.main.output
     //if project name is BBM-Dev it makes the deobf jar deobf-dev if not its just deobf
     classifier = "deobf"
     //sets where the jars go to.
     destinationDir = file 'output'
     manifest {
        attributes 'FMLCorePlugin': 'zmaster587.advancedRocketry.asm.AdvancedRocketryPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }
     mustRunAfter "jar" 
}

curseforge{
  if (project.hasProperty("thecursedkey")) {
        apiKey = project.thecursedkey
    }
    else {
    apiKey = ""
    }
  project {
  //AdvancedRocketry
    id = "236542"
    relations {
        requiredLibrary 'libvulpes'
    }
    changelog = file('changelog.html')
    changelogType = 'html'
    releaseType = "alpha"
    addGameVersion minecraft.version
    mainArtifact(jar) {
      displayName = "AdvancedRocketry ${project.version}-UNSTABLE build " + project.getBuildNumber() +  " for " + minecraft.version
    }
    addArtifact (deobfJar){
    	 displayName = "AdvancedRocketry ${project.version}-UNSTABLE-deobf build " + project.getBuildNumber() + " for " + minecraft.version
    }
   }
}

task makeChangelog(type: GitChangelogTask) {
    file = new File ("changelog.html")
    untaggedName = "Current Release ${project.version}"
    fromCommit = startGitRev
    toRef =  "HEAD"
    gitHubIssuePattern = "nonada123"
    templateContent = """
{{#tags}}
<h1> Highly untested and may break world</h1>
  <h3>{{name}}</h3>
  <ul>
    {{#commits}}
    <li> <a href="https://github.com/zmaster587/AdvancedRocketry/commit/{{hash}}" target=_blank> {{{message}}}</a>
</li>
    {{/commits}}
  </ul>
{{/tags}}
"""
}

tasks.build.dependsOn makeChangelog
/*
buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service' 
    licenseAgree = 'yes'                                        

    publishAlways() 

    if (System.getenv('CI')) {
        tag 'CI'
    } else {
        tag 'Local'
    }

    tag System.getProperty('os.name')                                            
}
*/
